namespace UpdateControls.Correspondence.UnitTest.Model;

fact Machine {
key:
	unique;
	
query:
	LogOn *activeLogOns {
		LogOn l : l.machine = this
			where l.isActive
	}
}

fact User {
key:
	string userName;

mutable:
	publish string favoriteColor;

query:
	Player* activePlayers {
		Player p : p.user = this
			where p.isActive
	}

	Player* finishedPlayers {
		Player p : p.user = this
			where p.isNotActive
	}

	UserFavoriteColor2* favoriteColor2 {
		UserFavoriteColor2 c : c.user = this
			where c.isCurrent
	}
}

fact UserFavoriteColor2 {
key:
	User user;
	string value;
	UserFavoriteColor2* prior;

query:
	bool isCurrent {
		not exists UserFavoriteColor2 next : next.prior = this
	}
}

fact LogOn {
key:
	unique;
	User user;
	Machine machine;
	
query:
	bool isActive {
		not exists LogOff lo : lo.logOn = this
	}
}

fact LogOff {
key:
	LogOn logOn;
}

fact Game {
key:
	unique;

query:
	Player* players {
		Player p : p.game = this
	}

	Move* moves {
		Move m : m.player.game = this
	}

	Outcome* outcomes {
		Outcome o : o.game = this
	}
}

fact GameName {
key:
	Game game;
	string name;
	GameName* prior;
}

fact Player {
key:
	publish User user;
	publish Game game;
	int index;

query:
	Move* moves {
		Move m : m.player = this
	}

	bool isActive {
		not exists Outcome o : o.game = this.game
	}

	bool isNotActive {
		exists Outcome o : o.game = this.game
	}
}

fact Move {
key:
	Player player;
	int index;
	int square;
}

fact Outcome {
key:
	Game game;
	Player? winner;
}
