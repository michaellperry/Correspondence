namespace Reversi.Model;

fact Machine {
	unique;
	
	LogOn *activeLogOns {
		LogOn l : l.machine = this
			where l.isActive
	}
}

fact User {
	string userName;

	Player* activePlayers {
		Player p : p.user = this
			where p.isActive
	}

	Player* finishedPlayers {
		Player p : p.user = this
			where p.isNotActive
	}
}

fact LogOn {
	unique;
	User user;
	Machine machine;
	
	bool isActive {
		not exists LogOff lo : lo.logOn = this
	}
}

fact LogOff {
	LogOn logOn;
}

fact Game {
	unique;

	Player* players {
		Player p : p.game = this
	}

	Move* moves {
		Move m : m.player.game = this
	}

	Outcome* outcomes {
		Outcome o : o.game = this
	}
}

fact Player {
	pivot User user;
	pivot Game game;
	int index;

	Move* moves {
		Move m : m.player = this
	}

	bool isActive {
		not exists Outcome o : o.game = this.game
	}

	bool isNotActive {
		exists Outcome o : o.game = this.game
	}
}

fact Move {
	Player player;
	int index;
	int square;
}

fact Outcome {
	Game game;
	Player? winner;
}
